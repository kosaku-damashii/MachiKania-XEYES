REM XEYES.BAS

USEGRAPHIC 5

USECLASS CRDINI

USEVAR LCDTURN
CRDINI::INIT()
IF CRDINI::ISSET("HORIZONTAL") THEN
  LCDTURN=0
ELSEIF CRDINI::ISSET("VERTICAL") THEN
  LCDTURN=270
ELSEIF CRDINI::ISSET("LCD0TURN") THEN
  LCDTURN=0
ELSEIF CRDINI::ISSET("LCD90TURN") THEN
  LCDTURN=90
ELSEIF CRDINI::ISSET("LCD180TURN") THEN
  LCDTURN=180
ELSEIF CRDINI::ISSET("LCD270TURN") THEN
  LCDTURN=270
ELSE
  LCDTURN=0
ENDIF

USEVAR X1,X2,Y1,Y2,XMIN,XMAX,YMIN,YMAX
XMIN=0X17A:XMAX=0XECB:YMIN=0X11A:YMAX=0XE81

USEVAR INITF,BGN,CLR,CLRD,PEN,WMAX,HMAX,OX,OY,LPX,LPY,LXD,LYD,RPX,RPY,RXD,RYD,ZD

CLS
SPI 2500
INITF$="TOUCHPAD.INI"

IF FOPEN(INITF$,"R") THEN
  XMIN=VAL(FINPUT$())
  XMAX=VAL(FINPUT$())
  YMIN=VAL(FINPUT$())
  YMAX=VAL(FINPUT$())
  FCLOSE
ELSE
  GOSUB MINMAX
ENDIF

BGN=0
PEN=1
WMAX=SYSTEM(22)
HMAX=SYSTEM(23)

BOXFILL 0,0,WMAX-1,HMAX-1,15
OX=50
OY=60

Z=GOSUB(ELFILL,OX,OY,35,50,0)
Z=GOSUB(ELFILL,OX,OY,28,40,7)

Z=GOSUB(ELFILL,OX+80,OY,35,50,0)
Z=GOSUB(ELFILL,OX+80,OY,28,40,7)

LPX=0
RPX=0

DO
  IF IN(0) THEN
    BGN=1
    CONTINUE
  ENDIF
  X1=GOSUB(TSCREAD,0X94)
  Y1=GOSUB(TSCREAD,0XD4)
  X2=GOSUB(TSCREAD,0X94)
  Y2=GOSUB(TSCREAD,0XD4)
  IF IN(0) THEN
    CONTINUE
  ENDIF
  IF ABS(X1-X2)>2 THEN CONTINUE
  IF ABS(Y1-Y2)>2 THEN CONTINUE
  X=(X1+X2)>>1
  Y=(Y1+Y2)>>1
  X=WMAX*(X-XMIN)/(XMAX-XMIN)-1
  Y=HMAX*(Y-YMIN)/(YMAX-YMIN)-1

  IF 0=LCDTURN THEN
    IF BGN=1 THEN
      BGN=0
    ENDIF
    REM 319-X,239-Y
    PRINT "Not supported."
    BREAK
  ELSEIF 90=LCDTURN THEN
    IF BGN=1 THEN
      BGN=0
    ENDIF
    REM 239-Y,X
    PRINT "Not supported."
    BREAK
  ELSEIF 180=LCDTURN THEN
    IF BGN=1 THEN
      BGN=0
    ELSE
      IF LPX != 0 THEN Z=GOSUB(ELFILL,LPX,LPY,6 ,8 ,7)
      LXD#=FLOAT#(X-OX)
      LYD#=FLOAT#(Y-OY)
      ZD#=SQRT#(LXD#*LXD#+LYD#*LYD#)
      LPX=OX+INT(LXD#*20/ZD#)
      LPY=OY+INT(LYD#*20/ZD#)
      Z=GOSUB(ELFILL,LPX,LPY,6 ,8 ,0)

      IF RPX != 0 THEN Z=GOSUB(ELFILL,RPX+80,RPY,6 ,8 ,7)
      RXD#=FLOAT#(X-OX-80)
      RYD#=FLOAT#(Y-OY)
      ZD#=SQRT#(RXD#*RXD#+RYD#*RYD#)
      RPX=OX+INT(RXD#*20/ZD#)
      RPY=OY+INT(RYD#*20/ZD#)
      Z=GOSUB(ELFILL,RPX+80,RPY,6 ,8 ,0)
    ENDIF
  ELSE
    IF BGN=1 THEN
      BGN=0
    ENDIF
    REM Y,319-X
    PRINT "Not supported."
    BREAK
  ENDIF
LOOP
END

USEVAR CX,CY,RX,RY,COL,XX,YY

LABEL EL
  VAR CX,CY,RX,RY,COL,XX,YY,X,Y
  CX=ARGS(1)
  CY=ARGS(2)
  RX=ARGS(3)
  RY=ARGS(4)
  COL=ARGS(5)

  XX=RX*64
  YY=0
  X=0
  Y=0
  WHILE XX>=0
    X=XX/64
    Y=YY/64
    PSET CX+X,CY+Y,COL
    PSET CX-X,CY+Y,COL
    PSET CX-X,CY-Y,COL
    PSET CX+X,CY-Y,COL
    YY=YY+(XX*RY/RX/64)
    XX=XX-(YY*RX/RY/64)
  WEND
RETURN

LABEL ELFILL
  VAR CX,CY,RX,RY,COL,XX,YY,X,Y
  CX=ARGS(1)
  CY=ARGS(2)
  RX=ARGS(3)
  RY=ARGS(4)
  COL=ARGS(5)

  XX=RX*64
  YY=0
  X=0
  Y=0
  WHILE XX>=0
    X=XX/64
    Y=YY/64
    LINE CX+X,CY+Y,CX-X,CY+Y,COL
    LINE CX-X,CY-Y,CX+X,CY-Y,COL
    YY=YY+(XX*RY/RX/64)
    XX=XX-(YY*RX/RY/64)
  WEND
RETURN

LABEL TSCREAD
  REM I:COUNTER, D: DATA, R: RESULT, N:MINIMUM, X:MAXIMUM
  VAR I,D,R,N,X
  R=0
  N=0XFFFF
  X=0
  FOR I=1 TO 10
    D=ARGS(1)
    SPISWAPDATA &D,3
    D=((D AND 0X7F00)>>3) OR ((D AND 0XF80000)>>19)
    R=R+D
    IF D<N THEN N=D
    IF X<D THEN X=D
  NEXT
  RETURN (R-N-X)>>3

LABEL MINMAX
  IF LCDTURN=0 OR LCDTURN=180 THEN
    LINE 0,0,30,30:LINE 0,0,10,0:LINE 0,0,0,10
    LINE 289,209,319,239:LINE 309,239,319,239:LINE 319,229,319,239
    CURSOR 0,15
    PRINT "DRAW LINES ON THE ALLOWS SEVERAL TIMES."
    PRINT "THEN, PRESS FIRE BUTTON."
  ELSE
    LINE 239,0,209,30:LINE 239,0,229,0:LINE 239,0,239,10
    LINE 0,319,30,289:LINE 0,319,10,319:LINE 0,319,0,309
    CURSOR 0,15
    PRINT "DRAW LINES ON THE ALLOWS "
    PRINT "SEVERAL TIMES."
    PRINT "THEN, PRESS FIRE BUTTON."
  ENDIF
  XMIN=0XFFFF:XMAX=0:YMIN=0XFFFF:YMAX=0
  DO
    IF KEYS(32) THEN BREAK
    IF IN(0) THEN CONTINUE
    X1=GOSUB(TSCREAD,0X94)
    Y1=GOSUB(TSCREAD,0XD4)
    X2=GOSUB(TSCREAD,0X94)
    Y2=GOSUB(TSCREAD,0XD4)
    IF IN(0) THEN CONTINUE
    IF ABS(X1-X2)>2 THEN CONTINUE
    IF ABS(Y1-Y2)>2 THEN CONTINUE
    X=(X1+X2)>>1
    Y=(Y1+Y2)>>1
    IF X<XMIN THEN XMIN=X
    IF XMAX<X THEN XMAX=X
    IF Y<YMIN THEN YMIN=Y
    IF YMAX<Y THEN YMAX=Y
    CURSOR 5,5:PRINT HEX$(XMIN),HEX$(XMAX),
    CURSOR 5,6:PRINT HEX$(YMIN),HEX$(YMAX),
  LOOP
  FOPEN(INITF$,"W")
  FPRINT XMIN
  FPRINT XMAX
  FPRINT YMIN
  FPRINT YMAX
  FCLOSE
RETURN
